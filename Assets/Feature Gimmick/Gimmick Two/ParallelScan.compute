
#pragma kernel GlobalBlockScan

#define THREAD_SIZE 256

StructuredBuffer<uint> _localBlockSumsBuffer;
RWStructuredBuffer<uint> _blockSumsScanBuffer;

groupshared uint _blockSumsScanData[THREAD_SIZE];
int _bitShift;

[numthreads(THREAD_SIZE, 1, 1)]
void GlobalBlockScan(uint3 tid : SV_DispatchThreadID, uint3 gtid : SV_GroupThreadID, uint gid : SV_GroupID)
{
	uint offset = 1;
	_blockSumsScanData[tid.x] = _localBlockSumsBuffer[tid.x];
	GroupMemoryBarrierWithGroupSync();

	[unroll]
	for (uint d = THREAD_SIZE >> 1; d > 0; d >>= 1)
	{
		if (gtid.x < d)
		{
			uint ai = offset * (2 * gtid.x + 1) - 1;
			uint bi = offset * (2 * gtid.x + 2) - 1;

			_blockSumsScanData[bi] += _blockSumsScanData[ai];
		}
		GroupMemoryBarrierWithGroupSync();

		offset <<= 1;
		GroupMemoryBarrierWithGroupSync();
	}

	if (gtid.x == 0)
	{
		_blockSumsScanData[THREAD_SIZE - 1] = 0;
	}
	GroupMemoryBarrierWithGroupSync();

	[unroll]
	for (uint d = 1; d < THREAD_SIZE; d <<= 1)
	{
		offset >>= 1;
		GroupMemoryBarrierWithGroupSync();

		if (gtid.x < d)
		{
			uint ai = offset * (2 * gtid.x + 1) - 1;
			uint bi = offset * (2 * gtid.x + 2) - 1;

			uint t = _blockSumsScanData[ai];
			_blockSumsScanData[ai] = _blockSumsScanData[bi];
			_blockSumsScanData[bi] += t;
		}
		GroupMemoryBarrierWithGroupSync();
	}
	GroupMemoryBarrierWithGroupSync();
	
	_blockSumsScanBuffer[tid.x] = _blockSumsScanData[tid.x];
	GroupMemoryBarrierWithGroupSync();
}